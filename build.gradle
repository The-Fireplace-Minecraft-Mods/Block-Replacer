
buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Modmuss50"
            url = "http://maven.modmuss50.me/"
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'ModsIOUpload:ModsIOUpload:+'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.2.0"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'net.cazzar.gradle.modsio'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

version = "2.1.0"
group= "the_fireplace.wgblockreplacer"
archivesBaseName = "WGBlockReplacer-1.13.2"

minecraft {
    mappings channel: 'stable', version: '43-1.13'
    
    runs {
        client = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
    }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.13.2-25.0.74'
}

repositories {
}

jar {
    manifest {
        attributes(["Specification-Title": "wgblockreplacer",
                    "Specification-Vendor": "The_Fireplace",
                    "Specification-Version": "24.0", // We are version 1 of the modlauncher specification
                    "Implementation-Title": "WorldGen Block Replacer",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" : "The_Fireplace",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives jar
    archives deobfJar
}

curseforge {
  apiKey = project.hasProperty("curseForgeApiKey")? project.curseForgeApiKey : "empty"
  project {
    id = '287321'
    changelog = file('changelog.txt')
    releaseType = 'beta'
    addGameVersion '1.13.2'
	addGameVersion 'Java 8'
    mainArtifact(jar) {
        displayName = jar.archiveName.replace(".jar", "")
    }
    addArtifact(deobfJar) {
        displayName = deobfJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
  }
}

modsIO {
    key = project.hasProperty("modsioApiKey")? project.modsioApiKey : "empty"
    project{
		artifact = new File(jar.destinationDir, archivesBaseName+"-"+version+".jar")
		modid = "1276"
		minecraft = "1.13.2"
		try{changelog = file('changelog.txt').text}catch(Exception e){changelog=""}
		tag = "beta"
		current = true
    }
}
